#!/bin/env zsh
source $^fpath/prompt_framework(N)

prompt_codesmith_setup() {
    setopt localoptions noshwordsplit prompt_subst

    # Load required modules
    zmodload zsh/datetime
    zmodload zsh/parameter

    unset PS1 PS2 RPS1

    prompt_clean_zstyles
    prompt_clean_add_hooks

    if [[ -n $2 ]]; then
        prompt_clean_pwd_length=$2
    else
        prompt_clean_pwd_length='1'
    fi

    local prompt_sym root_prompt_sym prompt_username term
    zstyle -s ':clean:normal:render' prompt-symbol prompt_sym || prompt_sym='â€º'
    zstyle -s ':clean:root:render' prompt-symbol root_prompt_sym || root_prompt_sym='#'
    if [[ -n $1 ]]; then
        [[ $1 =~ '256color$' ]]; term=$?
    else
        [[ $TERM =~ '256color$' ]]; term=$?
    fi

    # show username@host if logged in through SSH
    if [[ $SSH_CONNECTION != '' ]]; then
        if [[ $term -eq 0 ]]; then
            prompt_username='%F{83}%m%f:'
        else
            prompt_username='%F{10}%m%f:'
        fi
    fi

    # Construct the new prompt with a clean preprompt.
    local -ah ps1
    if [[ $term -eq 0 ]]; then
        ps1=(
            $prompt_newline # Initial newline, for spaciousness.
            $prompt_username
            '%F{248}in %F{45}%B%5v%f%b' # Path
            '%(1V. %F{244}on %B%F{%(2V.red.green)}%1v%b%F{yellow}%(3V. %3v.)%f.)' # VCS status
            '%(4V. %F{215}%4v%f.)' # Execution time
            $prompt_newline # Separate preprompt and prompt.
            "%F{%(?.white.red)}%(!.$root_prompt_sym.$prompt_sym)%f " # Prompt symbol
        )
        PS2="%F{244}%_ %F{white}%(!.#.$prompt_sym)%f "
    else
        ps1=(
            $prompt_newline # Initial newline, for spaciousness.
            $prompt_username
            '%F{14}%5v%f' # Path
            '%(1V. %F{%(3V.10.8)}%1v%2v%(3V. %3v.)%f.)' # VCS status
            '%(4V. %F{11}%4v%f.)' # Execution time
            $prompt_newline # Separate preprompt and prompt.
            "%F{%(?.13.9)}%(!.$root_prompt_sym.$prompt_sym)%f " # Prompt symbol
        )
        PS2="%F{8}%_ %F{14}%(!.#.$prompt_sym)%f "
    fi

    PS1="${(j..)ps1}"
}

prompt_codesmith_preview () {
    prompt_preview_theme codesmith
    prompt_preview_theme codesmith 256color
    prompt_preview_theme codesmith xterm 0
    prompt_preview_theme codesmith 256color 0
}

prompt_codesmith_setup "$@"

